plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.140'
    id 'idea'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

sourceSets {
    main {
        resources {
            srcDir 'src/generated/resources'
        }
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

repositories {
    flatDir { dirs 'src/jij' }
    maven { url = "https://maven.createmod.net" }
    maven { url = "https://maven.ithundxr.dev/snapshots" }
    maven { name = "TerraformersMC"; url = "https://maven.terraformersmc.com/" }
    exclusiveContent {
        forRepository { maven { name = "Modrinth"; url = "https://api.modrinth.com/maven" } }
        filter { includeGroup "maven.modrinth" }
    }
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    repositories {
        maven {
            name = "Illusive Soulworks maven"
            url = "https://maven.theillusivec4.top/"
        }
    }
}

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // gameDirectory = project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }

    mods {
        utm {sourceSet sourceSets.main}
    }
}

dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false }
    implementation("net.createmod.ponder:ponder-neoforge:${ponder_version}+mc${minecraft_version}")
    compileOnly("dev.engine-room.flywheel:flywheel-neoforge-api-${minecraft_version}:${flywheel_version}")
    runtimeOnly("dev.engine-room.flywheel:flywheel-neoforge-${minecraft_version}:${flywheel_version}")
    implementation("com.tterrag.registrate:Registrate:${registrate_version}")

    // EMI+JEI
    compileOnly("dev.emi:emi-neoforge:${emi_version}:api")
    runtimeOnly("dev.emi:emi-neoforge:${emi_version}")
    compileOnly("mezz.jei:jei-1.21.1-neoforge-api:19.27.0.340")
    runtimeOnly("maven.modrinth:jei:YAcQ6elZ")

    jarJar(implementation("maven.modrinth:extreme_sound_muffler:KXmiEin1")) // extreme sound muffler
    implementation("maven.modrinth:backported-spears:uUy23VAp") // backported spears
    implementation("maven.modrinth:showcase-item:AD6eBCKn") // showcase item
    compileOnly("maven.modrinth:figura:yXFi1fed") // figura
    implementation("maven.modrinth:gravestone-mod:AZm51eX1") // gravestones
    implementation("maven.modrinth:sophisticated-backpacks:ovVp31Ci") // sophisticated backpacks
    implementation("maven.modrinth:sophisticated-core:23alQult") // sophisticated core
    jarJar(implementation("maven.modrinth:sophisticated-backpacks-create-integration:bWd7B59k")) // sophisticated backpacks x create
    jarJar(implementation('local:grimble_mod:1.0.0')) // bluebibbob's grimble mod

    // curios
    compileOnly "top.theillusivec4.curios:curios-neoforge:9.5.1+1.21.1:api"
    runtimeOnly "top.theillusivec4.curios:curios-neoforge:9.5.1+1.21.1"
}

def replaceProperties = [
        minecraft_version      : minecraft_version,
        minecraft_version_range: minecraft_version_range,
        neo_version            : neo_version,
        loader_version_range   : loader_version_range,
        mod_id                 : mod_id,
        mod_name               : mod_name,
        mod_license            : mod_license,
        mod_version            : mod_version,
]

def generateMainMetadata = tasks.register("generateMainMetadata", ProcessResources) {
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata/main"
}

sourceSets.main.resources.srcDir generateMainMetadata
neoForge.ideSyncTask generateMainMetadata


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named('jarJar') {}

tasks.register('cleanupVersionedJars', Delete) {
    delete layout.buildDirectory.dir("libs").map { dir ->
        dir.asFileTree.matching { include "*-${mod_version}*.jar" }
    }
}

def registerRename(String taskName, String oldName, String newName) {
    def libDirProvider = layout.buildDirectory.dir("libs")

    tasks.named(taskName) {
        doLast {
            def libDir = libDirProvider.get().asFile
            def oldFile = new File(libDir, oldName)
            def newFile = new File(libDir, newName)

            if (oldFile.exists()) {
                if (newFile.exists()) {
                    newFile.delete()
                }

                if (oldFile.renameTo(newFile)) {
                    logger.lifecycle("Successfully renamed ${oldName} to ${newName}")
                } else {
                    throw new GradleException("Failed to rename ${oldName}. File might be locked.")
                }
            }
        }
        finalizedBy('cleanupVersionedJars')
    }
}

registerRename('jar', "utm-${mod_version}.jar", "utm.jar")