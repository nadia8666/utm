plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.140'
    id 'idea'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

sourceSets {
    main {
        java { srcDir 'src/main/java' }
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/generated/resources'
        }
    }
    updater {
        java { srcDir 'src/updater/java' }
        resources {
            srcDir 'src/updater/resources'
        }

        compileClasspath += sourceSets.main.output
    }
}

repositories {
    maven { url = "https://maven.createmod.net" }
    maven { url = "https://maven.ithundxr.dev/snapshots" }
    maven { name = "TerraformersMC"; url = "https://maven.terraformersmc.com/" }
    exclusiveContent {
        forRepository { maven { name = "Modrinth"; url = "https://api.modrinth.com/maven" } }
        filter { includeGroup "maven.modrinth" }
    }
}

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    addModdingDependenciesTo(sourceSets.updater)

    mods {
        utm { sourceSet(sourceSets.main) }
        utm_updater { sourceSet(sourceSets.updater) }
    }

    runs {
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG

            taskBefore tasks.named('processResources')
            taskBefore tasks.named('processUpdaterResources')
            taskBefore tasks.named('compileJava')
            taskBefore tasks.named('compileUpdaterJava')
        }

        client {
            client()
            loadedMods = [mods.utm, mods.utm_updater]
            systemProperty 'neoforge.enabledGameTestNamespaces', "utm,utm_updater"
        }

        server {
            server()
            programArgument '--nogui'
            loadedMods = [mods.utm, mods.utm_updater]
        }
    }
}

dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false }
    implementation("net.createmod.ponder:ponder-neoforge:${ponder_version}+mc${minecraft_version}")
    compileOnly("dev.engine-room.flywheel:flywheel-neoforge-api-${minecraft_version}:${flywheel_version}")
    runtimeOnly("dev.engine-room.flywheel:flywheel-neoforge-${minecraft_version}:${flywheel_version}")
    implementation("com.tterrag.registrate:Registrate:${registrate_version}")

    compileOnly("dev.emi:emi-neoforge:${emi_version}:api")
    runtimeOnly("dev.emi:emi-neoforge:${emi_version}")

    jarJar(implementation("maven.modrinth:5IIKsxiL:KXmiEin1"))
}

def replaceProperties = [
        minecraft_version      : minecraft_version,
        minecraft_version_range: minecraft_version_range,
        neo_version            : neo_version,
        loader_version_range   : loader_version_range,
        mod_id                 : mod_id,
        mod_name               : mod_name,
        mod_license            : mod_license,
        mod_version            : mod_version,
]

tasks.withType(ProcessResources).configureEach {
    inputs.properties replaceProperties
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta']) {
        expand replaceProperties
    }
}

def generateMainMetadata = tasks.register("generateMainMetadata", ProcessResources) {
    var props = [minecraft_version: minecraft_version, neo_version: neo_version, mod_id: mod_id, mod_version: mod_version, mod_name: mod_name]
    inputs.properties props
    expand props
    from "src/main/templates"
    into "build/generated/sources/modMetadata/main"
}

def generateUpdaterMetadata = tasks.register("generateUpdaterMetadata", ProcessResources) {
    var props = [minecraft_version: minecraft_version, neo_version: neo_version, mod_id: "utm_updater", mod_version: mod_version, mod_name: "${mod_name} Updater"]
    inputs.properties props
    expand props
    from "src/updater/templates" // Use same templates, but different output
    into "build/generated/sources/modMetadata/updater"
}

sourceSets.main.resources.srcDir generateMainMetadata
sourceSets.updater.resources.srcDir generateUpdaterMetadata
neoForge.ideSyncTask generateMainMetadata
neoForge.ideSyncTask generateUpdaterMetadata

tasks.withType(ProcessResources).configureEach {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.register('updaterJar', Jar) {
    from sourceSets.updater.output
    archiveClassifier = 'updater'
}

tasks.named('assemble') {
    dependsOn 'updaterJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}